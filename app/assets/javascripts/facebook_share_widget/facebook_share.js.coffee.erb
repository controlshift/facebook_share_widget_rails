#= require underscore.js
#= require backbone.js
#= require handlebars-runtime.js
#= require facebook_share_widget/sharing_message

$ ->
  class NameSearch extends Backbone.Model
    searchFilter: ->
      @get('filter')

    setSearchFilter: (content)->
      @set('filter': content)

  class FacebookFriends extends Backbone.Collection
    url: '/widget/facebook/friends'
    model: app.models.FacebookFriend

    filterBy: (criteria) ->
      unless criteria
        return this.models
      @criteria = criteria
      @filter(@nameMatched)

    nameMatched: (friend) =>
      regex = new RegExp(@criteria, "i")
      regex.test(friend.get('name'))

  class SharingMessageView extends Backbone.View
    events:
      'blur .message' : 'update'

    render: ->
      $(@el).html(HandlebarsTemplates['facebook_share_widget/templates/sharing_message']())
      this

    update: (options) =>
      @model.set('msg', $('.message').val())

  class NameSearchView extends Backbone.View
    events:
      'keyup .search-text' : 'filterUpdate'

    render: ->
      $(@el).html(HandlebarsTemplates['facebook_share_widget/templates/name_search']())
      $(@el).children('.search-text').before("<img src='<%= asset_path("facebook_share_widget/facebook-logo.png") %>'/>")
      this

    filterUpdate: ->
      @model.setSearchFilter($('input.search-text').val())

  class FacebookFriendsView extends Backbone.View
    initialize: ->
      @loading()
      @sharingMessage = new app.models.SharingMessage
      @sharingMessageView = new SharingMessageView(el: $('.message-pane'), model: @sharingMessage)
      @sharingMessageView.render()

      @nameSearch = new NameSearch
      @nameSearch.on('change', @render, this)
      @nameSearchView = new NameSearchView(el: $('.name-search'), model: @nameSearch)
      @nameSearchView.render()

      @collection = new FacebookFriends
      @collection.on("reset", @render, this)
      @collection.on("reset", @bindMessageModel, this)

    load: =>
      self = this
      @collection.fetch({ data: $.param({ link: $.parseJSON($('.template').text()).link }) }).complete ->
        self.render()

    setMessageModel: (friend) =>
      friend.setMessageModel(@sharingMessage)

    bindMessageModel: ->
      @collection.each(@setMessageModel)

    render: =>
      this.$el.html(HandlebarsTemplates['facebook_share_widget/templates/all_friends']())
      filteredFriends = @collection.filterBy(@nameSearch.searchFilter())
      @appendFriend friend for friend in filteredFriends

    loading: =>
      this.$el.html(HandlebarsTemplates['facebook_share_widget/templates/all_friends']())
      this.$el.children('#friends').append("<img src='<%= asset_path("facebook_share_widget/loader.gif") %>'/>")

    appendFriend: (friend) ->
      friendView = new app.views.FacebookFriendView(model: friend)
      @$('#friends').append(friendView.render().el)

  if $('#container').length
    f = new FacebookFriendsView(el: $('#container'))
    window.delayed_load = f.load
    setTimeout(window.delayed_load, 100)
