root = exports ? this

class @FacebookShareWidget
  throttleRate: 250
  friends: []
  base_path: ""
  template: {}
  
  constructor: (@container, @options) ->
    self = this
    self.base_path = @options.base_path
    self.template = @options.template if @options.template?

    $.ajax
      url: self.base_path + "/facebook/friends"
      type: "get"
      dataType: "json"
      data:  $.extend({}, self.template)

      success: (resp) ->
        self.friends = resp
        $("ul", self.container).html(friendsToListItems.call(self))
      error: (jqXHR, textStatus, errorThrown) ->
        json = eval("(#{jqXHR.responseText})")
        #console.debug(json)
        
    rate = @throttleRate
    $('input.search-text', @container).keydown(throttle(self, filter_list, rate))
    
    $("ul", @container).on "click", "a", ->
      id = $(this).data("facebook-id").toString()
      friend = friendWithId.call(self, id)
      message = $('textarea.message', self.container).val()

      errorMessage = $(this).siblings(".error-message")
      indicator = $("<img class='indicator' src=\"<%= asset_path('facebook_share_widget/loader.gif') %>\" />").insertBefore($(this))
      $(this).remove()
      friend.status = "loading"

      data = $.extend({}, self.template)
      data.facebook_id = id
      data.message = message

      $.ajax
        url: self.base_path + "/facebook/share"
        type: "post"
        data: { post: data }
        success: (resp) ->
          indicator.attr src: '<%= asset_path("facebook_share_widget/tick.png") %>'
          friend.status = "loaded"
          root.facebookShareWidget.callbacks.success(friend)
        error: (jqXHR, textStatus, errorThrown) ->
          json = eval("(#{jqXHR.responseText})")
          #console.debug(json)
          indicator.attr src: '<%= asset_path("facebook_share_widget/cross.png") %>'
          errorMessage.text(json.message)
          friend.status = null
          root.facebookShareWidget.callbacks.fail(friend)

  friendWithId = (id) ->
    for friend in @friends
      return friend if friend.id == id

  friendsToListItems = ->
    html = ""
    for friend in @friends
      html += friendToListItem(friend)
    html

  friendToListItem = (item) ->
    html = "<li>"
    html += "<img class='profile' src='http://graph.facebook.com/#{item.id}/picture' />"
    html += "<span class='name'>#{item.name}</span>"
    html += "<span class='error-message'></span>"
    if item.status == "loading"
      html += "<img class='indicator' src=\"<%= asset_path('facebook_share_widget/loader.gif') %>\" />"
    else if item.status == "loaded"
      html += "<div class='indicator'><img src=\"<%= asset_path('facebook_share_widget/tick.png') %>\" /> Shared</div>"
    else
      html += "<a class='share-button' data-facebook-id='#{item.id}'>Share</a>"
    html += '</li>'
    
  filter_list = ->
    text = $('  input.search-text', @container).val()
    if text != undefined && text != null && text.length > 0
      regex = new RegExp(text, "i")
      html = ""
      for friend in @friends
        if regex.test(friend.name)
          html += friendToListItem(friend)
      $("ul", @container).html(html)
    else
      $("ul", @container).html(friendsToListItems.call(this))
      
  throttle = (context, fn, delay) ->
    timer = null
    ->
      args = arguments
      clearTimeout(timer)
      callback = -> fn.apply(context, args)
      timer = setTimeout(callback, delay)
    
root.facebookShareWidget =
  callbacks:
    success: (friend) ->
    fail: (friend) ->
    